dnl    Configure check stuff for TaskJuggler
dnl    Copyright (c) 2002 Chris Schlaeger <cs@suse.de>
dnl    Copyright (c) 2002 Klaas Freitag <freitag@suse.de>

dnl    This file is free software; you can redistribute it and/or
dnl    modify it under the terms of version 2 of the GNU General
dnl    Public License as published by the Free Software Foundation.

dnl    This library is distributed in the hope that it will be useful,
dnl    but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl    GNU General Public License for more details.

dnl    You should have received a copy of the GNU General Public License
dnl    along with this library; see the file COPYING.  If not, write to
dnl    the Free Software Foundation, Inc., 59 Temple Place - Suite 330,
dnl    Boston, MA 02111-1307, USA.

dnl The following part was taken from the KDE admin directory
dnl Start of configure.in.min

AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

dnl This ksh/zsh feature conflicts with `cd blah ; pwd`
unset CDPATH

dnl Checking host/target/build systems, for make, install etc.
AC_CANONICAL_SYSTEM 
dnl Perform program name transformation
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(taskjuggler, [2.0.1])

dnl AM_INIT_AUTOMAKE(@MODULENAME@, @VERSION@) dnl searches for some needed programs

dnl KDE_SET_PREFIX

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs.
AC_CHECK_COMPILERS
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
KDE_PROG_LIBTOOL

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files
dnl AM_KDE_WITH_NLS

dnl KDE_USE_QT
dnl AC_PATH_KDE

dnl End of configure.in.min

AC_MSG_CHECKING([for KDE support])
AC_ARG_WITH(kde-support,
        [  --with-kde-support[={yes|no}] Integrate KDE support [default=no]],
        [
                case "${withval}" in
                        yes|no) cv_option_kdesupport=$withval;;
                        *)  AC_MSG_ERROR(bad value ${withval} for --with-kde-support) ;;
                esac
        ], [cv_option_kdesupport=no])
AC_MSG_RESULT([$cv_option_kdesupport])

AM_CONDITIONAL(include_KDE, test "$cv_option_kdesupport" = yes)
AM_CONDITIONAL(include_ARTS, false)
AC_PATH_QT

if test "$cv_option_kdesupport" = yes ;then
    AC_DEFINE(HAVE_KDE,1,[Define if you have KDE])
    KDE_CHECK_THREADING
    AC_PATH_KDE 

    # FIXME: Proper installation for ical lib, for a workaround add link options
    #        for your system here.
    KDE_LIBS=''
   dnl LIBICAL="-lical -lkcal"
   AC_SUBST(KDE_LIBS)
   DO_NOT_COMPILE="Contrib"
else
   kde_htmldir=${datadir}/taskjuggler
   DO_NOT_COMPILE="ktjview ktjview2 Contrib"
fi

AC_CHECK_PROG(PERL, perl, perl, no)

AC_DEFUN([CHECK_PERL_MODULE],[ 
AC_MSG_CHECKING([for Perl module $1])
TMPFILE=conftest.pl
rm -f $TMPFILE
cat >$TMPFILE <<'EOT'
use $1;
EOT
$PERL $TMPFILE 2> /dev/null
if test $? -eq 0 ; then
    $2="yes"
    AC_MSG_RESULT(found)
else
    $2="no"
    AC_MSG_RESULT(not found)
fi
rm -f $TMPFILE
])

dnl Check for the stuff the GANTT chart generator needs
CHECK_PERL_MODULE(Date::Calc, DATECALC)
CHECK_PERL_MODULE(XML::Parser, XMLPARSER)
CHECK_PERL_MODULE(Class::MethodMaker, CLASSMETHODMAKER)
CHECK_PERL_MODULE(PostScript::Simple, POSTSCRIPTSIMPLE)
AC_CHECK_PROG(POSTER, poster, poster, no)
if test "$DATECALC" = no -o "$XMLPARSER" = no -o \
        "$CLASSMETHODMAKER" = no -o "$POSTSCRIPTSIMPLE" = no -o \
        "$POSTER" = no ; then
  AC_MSG_WARN(tjx2gantt GANTT chart generator will not work)
  TJX2GANTT="no"
fi
AC_SUBST(TJX2GANTT)

AC_CHECK_PROG(XSLTPROC, xsltproc, xsltproc, no)
dnl Check for the stuff needed to create the HTML manual
AC_CHECK_PROG(DB2HTML, db2html, db2html, no)
if test "$XSLTPROC" = no -o "$DB2HTML" = no ; then
  AC_MSG_WARN([HTML manual will not be generated])
  HTMLMANUAL="no"
else
  HTMLMANUAL="yes"
fi
AC_SUBST(HTMLMANUAL)

dnl Check for the stuff needed to create the PS manual
AC_CHECK_PROG(DB2PS, db2ps, db2ps, no)
AC_CHECK_PROG(JADE, jade, jade, no)
AC_CHECK_PROG(JADETEX, jadetex, jadetex, no)
AC_CHECK_PROG(DVIPS, dvips, dvips, no)
if test "$XSLTPROC" = no -o "$DB2PS" = no -o \
        "$JADE" = no -o "$JADETEX" = no -o \
        "$DVIPS" = no ; then
  AC_MSG_WARN([Postscript manual will not be generated])
  PSMANUAL="no"
else
  PSMANUAL="yes"
fi
AC_SUBST(PSMANUAL)

AC_OUTPUT(docs/en/taskjuggler.xml)

dnl KDE_INIT_DOXYGEN([The TaskJuggler API Reference], [Version $VERSION])

